        
var c n w k R d b
     mu
     z;
    % xi ${\xi}$ (long_name='Financial Conditions');

varexo eps_z ${\varepsilon_z}$ (long_name='Technology shock')
%      eps_xi ${\varepsilon_{\xi}}$ (long_name='Financial Shock')
         ;

parameters theta ${\theta}$ (long_name='capital share')
        betta ${\beta}$ (long_name='discount factor')
        alppha ${\alpha}$ (long_name='disutility from work')
        delta ${\delta}$ (long_name='depreciation')
        tau ${\tau}$ (long_name='tax wedge')
        kappa ${\kappa}$ (long_name='equity cost')
        siggma ${\sigma}$ (long_name='risk aversion')
        sigma_z ${\sigma_z}$ (long_name='std_z')
        sigma_xi ${\sigma_xi}$ (long_name='std_xi')
        A11 ${A_{11}}$ (long_name='A_11')
        A12 ${A_{12}}$ (long_name='A_12')
        xi
%         A21 ${A_{21}}$ (long_name='A_21')
%         A22 ${A_{22}}$ (long_name='A_22')
        ;
     
siggma=1;
theta = 0.36;
betta = 0.9825;
delta = 0.025;
tau = 0.35;        

    kappa = 0.146;       
    sigma_xi = 0.0098;  
    sigma_z = 0.0045; 
    A11 = 0.9457;
    A12 = 0;
    xi = 0.5;
%     A21 = 0;
%     A22 = 0.9703;

model;
[name='FOC labor, equation 1 on p. 4 Appendix']
w/c^siggma-alppha/(1-n)=0;
[name='Euler equatoin, equation 2 on p. 4 Appendix']
c^(-siggma)=betta*((R-tau)/(1-tau))*c(+1)^(-siggma);
[name='budget constraint household, equation 3 on p. 4 Appendix']
w*n+b(-1)-b/R+d-c=0;
[name='FOC labor input, equation 4 on p. 4 Appendix']
(1-theta)*z*k(-1)^theta*n^(-theta)=w*(1/(1-mu*(1+2*kappa*(d-steady_state(d)))));
[name='FOC capital, equation 5 on p. 4 Appendix']
betta*(c/(c(+1)))^siggma*((1+2*kappa*(d-steady_state(d)))/(1+2*kappa*(d(+1)-steady_state(d))))*(1-delta+(1-mu(+1)*(1+2*kappa*(d(+1)-steady_state(d))))*theta*z(+1)*k^(theta-1)*n(+1)^(1-theta))+xi*mu*(1+2*kappa*(d-steady_state(d)))=1;
[name='FOC bonds, equation 6 on p. 4 Appendix']
R*betta*(c/(c(+1)))^siggma*((1+2*kappa*(d-steady_state(d)))/(1+2*kappa*(d(+1)-steady_state(d))))+xi*mu*(1+2*kappa*(d-steady_state(d)))*(R*(1-tau)/(R-tau))=1;
[name='budget constraint firm, equation 7 on p. 4 Appendix']
(1-delta)*k(-1)+z*k(-1)^theta*n^(1-theta)-w*n-b(-1)+b/R-k-(d+kappa*(d-steady_state(d))^2)=0;
[name='Enforcement constraint, equation 8 on p. 4 Appendix']
xi*(k-b*((1-tau)/(R-tau)))=z*k(-1)^theta*n^(1-theta);
[name='Equation 1 of VAR in equation (11)']
log(z/steady_state(z))=A11*log(z(-1)/steady_state(z))+eps_z;
% [name='Equation 2 of VAR in equation (11)']
% log(xi/steady_state(xi))=A21*log(z(-1)/steady_state(z))+A22*log(xi(-1)/steady_state(xi))+eps_xi;
 
end;

shocks;
%     var eps_xi=sigma_xi^2;
    var eps_z= sigma_z^2;       
end;

steady;

stoch_simul(order=1,bandpass_filter=[6,32],irf=50);